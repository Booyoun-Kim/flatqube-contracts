pragma ton-solidity >= 0.62.0;

pragma AbiHeader time;
pragma AbiHeader expire;
pragma AbiHeader pubkey;

import "./libraries/OrderGas.tsol";
import "./libraries/OrderErrors.tsol";

import "./structures/IOrderRootCreateResult.tsol";

import "./interfaces/IOrderRoot.tsol";
import "./interfaces/IOrderFactory.tsol";
import "./interfaces/IOrderOperationCallback.tsol";

import "./Order.tsol";

import "@broxus/contracts/contracts/libraries/MsgFlag.tsol";
import "tip3/contracts/interfaces/ITokenRoot.tsol";
import "tip3/contracts/interfaces/ITokenWallet.tsol";
import "tip3/contracts/interfaces/IAcceptTokensTransferCallback.tsol";
import "ton-eth-bridge-token-contracts/contracts/TokenWalletPlatform.sol";

contract OrderRoot is IAcceptTokensTransferCallback, IOrderRoot  {
    address factory;
    address spentToken;
    
    uint32 version;
    TvmCell orderCode;
    TvmCell orderClosedCode;
    TvmCell tokenPlatformCode;

    address spentTokenWallet;
    address deployer;
    address dexRoot;

    OrderFeeParams fee;
	address beneficiary;
    mapping(address => OrderWithdrawalFeeParams) withdrawalFeeParams;

    constructor() public { revert(); }

    modifier onlyFactory() {
		require(
			msg.sender.value != 0 && msg.sender == factory,
			OrderErrors.NOT_FACTORY_LIMIT_ORDER_ROOT
		);
		_;
	}

    function onTokenWallet(address _wallet) external {
        require(
            msg.sender.value != 0 && msg.sender == spentToken,
            OrderErrors.NOT_TOKEN1_ROOT
        );
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);
        spentTokenWallet = _wallet;

        IOrderFactory(factory).onOrderRootDeployed{
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED
        }(address(this), spentToken, deployer);
    }

    function onTokenWalletReceive(address _wallet) external {}

    onBounce(TvmSlice body) external view {
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);
        uint32 functionId = body.decode(uint32);

        if (
            functionId == tvm.functionId(ITokenRoot.deployWallet) &&
            msg.sender == spentToken
        ){
            deployer.transfer(
                0,
                false,
                MsgFlag.ALL_NOT_RESERVED + MsgFlag.DESTROY_IF_ZERO
            );
        }
    }

    function getFeeParams() override external view responsible returns (OrderFeeParams params, address beneficiary) {
		return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } (fee, beneficiary);
	}

    function getVersion() override external view responsible returns (uint32) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } version;
    }

    function getSpentToken() override external view responsible returns(address) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } spentToken;
    }

    function getFactory() override external view responsible returns(address) {
        return { value: 0, bounce: false, flag: MsgFlag.REMAINING_GAS } factory;
    }

    function getTokenWallet(
        address token,
        address tokenReceiver
    ) internal view returns (address) {
        return
        address(tvm.hash(
            tvm.buildStateInit({
                contr: TokenWalletPlatform,
                varInit: { root: token, owner: tokenReceiver },
                code: tokenPlatformCode
        })
        ));
    }

    function setFeeParams(OrderFeeParams params) override external onlyFactory {
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);
        fee = params;
        factory.transfer(
            0,
            false,
            MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS
        );
    }

    function setBeneficiary(address beneficiary_) override external onlyFactory {
        require(beneficiary_.value != 0,
            OrderErrors.WRONG_BENEFICIARY);
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);
        beneficiary = beneficiary_;

        factory.transfer(
            0,
            false,
            MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS
        );
    }

    function withdrawFee(uint128 amount, address recipient, address tokenRoot, address rm_gas_to) override external onlyFactory {
        require(msg.value >= OrderGas.WITHDRAW_FEE_VALUE, OrderErrors.VALUE_TOO_LOW);
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);
        //withdrawalFeeParams[tokenRoot] = OrderWithdrawalFeeParams(0, recipient, amount, rm_gas_to);
//        ITokenRoot(tokenRoot).walletOf{
//            value: OrderGas.WITHDRAW_FEE_VALUE,
//            flag: MsgFlag.SENDER_PAYS_FEES,
//            callback: OrderRoot.onBeneficiaryWallet
//        }(beneficiary);

        address walletBeneficiary = getTokenWallet(tokenRoot, recipient);
        ITokenWallet(walletBeneficiary).transfer{
            value: OrderGas.WITHDRAW_FEE_VALUE,
            flag: MsgFlag.SENDER_PAYS_FEES,
            bounce: false
        }(
            amount,
            recipient,
            0,
            rm_gas_to,
            false,
            empty
        );
    }

    /*function onBeneficiaryWallet(address wallet) external {
        require(withdrawalFeeParams.exists(msg.sender), OrderErrors.WRONG_WITHDRAW_TOKEN_ROOT);
        OrderWithdrawalFeeParams params = withdrawalFeeParams[msg.sender];

        TvmCell empty;
        ITokenWallet(wallet).transfer{
        value: OrderGas.WITHDRAW_FEE_VALUE,
        flag: MsgFlag.SENDER_PAYS_FEES
        }(
            params.amount,
            params.recipient,
            0,
            params.rm_gas_to,
            false,
            empty
        );
        delete withdrawalFeeParams[msg.sender];
    }*/

    function buildPayload(
        uint64 callbackId,
        address tokenReceive,
        uint128 expectedTokenAmount,
        uint128 deployWalletValue,
        uint256 backPK
    ) external pure returns (TvmCell) {
        TvmBuilder builder;
        builder.store(callbackId);
        builder.store(tokenReceive);
        builder.store(expectedTokenAmount);
        builder.store(deployWalletValue);
        builder.store(backPK);

        return builder.toCell();
    }

    function onAcceptTokensTransfer(
        address tokenRoot,
        uint128 amount,
        address sender,
        address, /*senderWallet*/
        address originalGasTo,
        TvmCell payload
    ) external override {
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);
        TvmSlice payloadSlice = payload.toSlice();
        uint64 callbackId;
        if (payloadSlice.bits() >= 64) {
            callbackId = payloadSlice.decode(uint64);
        }

        if (payloadSlice.bits() == 779 &&
            msg.sender.value != 0 &&
            msg.sender.value >= OrderGas.DEPLOY_ORDER_MIN_VALUE &&
            msg.sender == spentTokenWallet) 
        {
            (address receiveToken, uint128 expectedAmount, uint128 deployWalletValue, uint256 backPubKey) =
            payloadSlice.decode(address, uint128, uint128, uint256);

            TvmCell indexCode = buildCode(
                receiveToken,
                orderCode
            );

            TvmCell stateInit = buildState(sender, indexCode, receiveToken);
            address orderAddress = new Order{
                stateInit: stateInit,
                value: OrderGas.DEPLOY_ORDER_MIN_VALUE
            }(
                expectedAmount,
                amount, 
                backPubKey, 
                dexRoot,
                fee,
                beneficiary,
                orderClosedCode
            );

            emit CreateOrder(orderAddress, tokenRoot, amount, receiveToken, expectedAmount);

            if (callbackId > 0) {
                IOrderOperationCallback(sender).onOrderCreateOrderSuccess{
                        value: OrderGas.OPERATION_CALLBACK_BASE,
                        flag: MsgFlag.SENDER_PAYS_FEES + MsgFlag.IGNORE_ERRORS,
                        bounce: false
                }(callbackId);
            }

            ITokenRoot(receiveToken).deployWallet { 
                value: OrderGas.DEPLOY_EMPTY_WALLET_VALUE,
                flag: MsgFlag.SENDER_PAYS_FEES,
                callback: OrderRoot.onTokenWalletReceive
            }(sender, OrderGas.DEPLOY_EMPTY_WALLET_GRAMS);

            ITokenWallet(msg.sender).transfer{
                value: 0, flag: MsgFlag.ALL_NOT_RESERVED,
                bounce: false
            }(
                amount,
                orderAddress,
                (deployWalletValue <= OrderGas.DEPLOY_EMPTY_WALLET_GRAMS? deployWalletValue:OrderGas.DEPLOY_EMPTY_WALLET_GRAMS),
                originalGasTo,
                true,
                payload
            );
        } else {
            emit CreateOrderReject();
            if (callbackId > 0){
                IOrderOperationCallback(sender).onOrderCreateOrderReject{
                        value: OrderGas.OPERATION_CALLBACK_BASE,
                        flag: MsgFlag.SENDER_PAYS_FEES + MsgFlag.IGNORE_ERRORS,
                        bounce: false
                }(callbackId);
            }

            TvmCell emptyPayload;
            ITokenWallet(msg.sender).transfer{
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED,
                bounce: false
            }(
                amount,
                sender, 
                uint128(0),
                originalGasTo,
                true,
                emptyPayload
            );
        }
    }

    function expectedAddressOrder(
        address _factory,
        address _root,
        address _owner,
        address _spentToken,
        address _receiveToken,
        uint64 timeTx,
        uint64 nowTx
    ) override external view responsible returns (address) 
    {
        TvmBuilder salt;
        salt.store(_root);
        salt.store(_receiveToken);

       return {
                value: 0,
                flag: MsgFlag.ALL_NOT_RESERVED,
                bounce: false
            } address(tvm.hash(
           tvm.buildStateInit({
               contr: Order,
               varInit: {
                    factory: _factory,
                    root: _root,
                    owner: _owner,
                    spentToken: _spentToken,
                    receiveToken: _receiveToken,
                    timeTx: timeTx,
                    nowTx: nowTx    
               },
               code: tvm.setCodeSalt(orderCode, salt.toCell())  
           })
       ));    
    }

    function buildCode(
        address _receiveToken,
        TvmCell _orderCode
    ) internal pure returns (TvmCell){
        TvmBuilder salt;
        salt.store(address(this));
        salt.store(_receiveToken);
        return tvm.setCodeSalt(_orderCode, salt.toCell());
    }

    function buildState(
        address sender,
        TvmCell _code,
        address _receiveToken
    ) internal view returns (TvmCell){
        return tvm.buildStateInit({
            contr: Order,
            varInit: {
                factory: factory,
                root: address(this),
                owner: sender,
                spentToken: spentToken,
                receiveToken: _receiveToken,
                timeTx: tx.timestamp,
                nowTx: uint64(now)
                },
            code: _code
        });
    }

    function upgrade(
        TvmCell _code, 
        uint32 _newVersion, 
        address _sendGasTo
    ) external override onlyFactory {
        if (version == _newVersion) {
            tvm.rawReserve(address(this).balance - msg.value, 0);
            _sendGasTo.transfer({ value: 0, flag: MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS });
        } else {
            emit OrderRootCodeUpgraded(_newVersion);

            TvmBuilder builder;
            builder.store(spentToken);
            builder.store(factory);
            builder.store(version);
            builder.store(orderCode);
            builder.store(orderClosedCode);
            builder.store(tokenPlatformCode);

            TvmBuilder builder1;
            builder1.store(spentTokenWallet);
            builder1.store(deployer);
            builder1.store(dexRoot);
            builder.storeRef(builder1);

            tvm.setcode(_code);
            tvm.setCurrentCode(_code);

            onCodeUpgrade(builder.toCell());
        }
    }

    function onCodeUpgrade(TvmCell _data) private {
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);

        TvmSlice sl = _data.toSlice();

        (address _factory, address _spentToken, uint32 oldVersion, uint32 _version, address _deployer) =
        sl.decode(address, address, uint32, uint32, address);

        if (oldVersion == 0) {
            tvm.resetStorage();
        }

        factory = _factory;
        beneficiary = _factory;
        spentToken = _spentToken;
        version = _version;
        deployer = _deployer;

        TvmSlice dataSl = sl.loadRefAsSlice();
        dexRoot = dataSl.decode(address);
        orderCode = dataSl.loadRef();
        orderClosedCode = dataSl.loadRef();

        tokenPlatformCode = dataSl.loadRef();
        (uint128 numerator_, uint128 denominator_) = dataSl.decode(uint128, uint128);
        fee = OrderFeeParams(numerator_, denominator_);
        tvm.rawReserve(OrderGas.TARGET_BALANCE, 0);

        ITokenRoot(spentToken).deployWallet{
            value: OrderGas.DEPLOY_EMPTY_WALLET_VALUE,
            flag: MsgFlag.SENDER_PAYS_FEES,
            callback: OrderRoot.onTokenWallet
        }(
            address(this), 
            OrderGas.DEPLOY_EMPTY_WALLET_GRAMS
        );

        deployer.transfer({
            value: 0,
            flag: MsgFlag.ALL_NOT_RESERVED + MsgFlag.IGNORE_ERRORS,
            bounce: false
        });
    }
}
